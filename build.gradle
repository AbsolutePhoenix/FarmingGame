plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = providers.gradleProperty('projectGroup').get()
version = providers.gradleProperty('projectVersion').get()

def lwjglVersion = providers.gradleProperty('lwjglVersion').get()
def steamworks4jserverVersion = providers.gradleProperty('steamworks4jserverVersion').get()
def steamworks4jVersion = providers.gradleProperty('steamworks4jVersion').get()
def jomlVersion = providers.gradleProperty('jomlVersion').get()
def jomlprimitivesVersion = providers.gradleProperty('jomlprimitivesVersion').get()
def lwjgl3awtVersion = providers.gradleProperty('lwjgl3awtVersion').get()

def lwjglModules = [
        "lwjgl", "lwjgl-assimp", "lwjgl-bgfx", "lwjgl-cuda", "lwjgl-egl", "lwjgl-fmod", "lwjgl-freetype", "lwjgl-glfw",
        "lwjgl-harfbuzz", "lwjgl-hwloc", "lwjgl-jawt", "lwjgl-jemalloc", "lwjgl-ktx", "lwjgl-libdivide", "lwjgl-llvm",
        "lwjgl-lmdb", "lwjgl-lz4", "lwjgl-meow", "lwjgl-meshoptimizer", "lwjgl-msdfgen", "lwjgl-nanovg", "lwjgl-nfd",
        "lwjgl-nuklear", "lwjgl-odbc", "lwjgl-openal", "lwjgl-opencl", "lwjgl-opengl", "lwjgl-opengles", "lwjgl-openvr",
        "lwjgl-openxr", "lwjgl-opus", "lwjgl-ovr", "lwjgl-par", "lwjgl-remotery", "lwjgl-rpmalloc", "lwjgl-shaderc",
        "lwjgl-spvc", "lwjgl-sse", "lwjgl-stb", "lwjgl-tinyexr", "lwjgl-tinyfd", "lwjgl-tootle", "lwjgl-vma",
        "lwjgl-vulkan", "lwjgl-xxhash", "lwjgl-yoga", "lwjgl-zstd"
]

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    lwjglModules.each { module ->
        implementation "org.lwjgl:${module}"
    }
    implementation "com.code-disaster.steamworks4j:steamworks4j-server:${steamworks4jserverVersion}"
    implementation "com.code-disaster.steamworks4j:steamworks4j:${steamworks4jVersion}"
    implementation "org.joml:joml:${jomlVersion}"
    implementation "org.joml:joml-primitives:${jomlprimitivesVersion}"
    implementation "org.lwjglx:lwjgl3-awt:${lwjgl3awtVersion}"
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}